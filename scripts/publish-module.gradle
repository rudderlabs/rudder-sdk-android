apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                // The coordinates of the library, being set from variables that
                // we'll set up later
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION

                from components.release
                artifact androidSourcesJar

                // Mostly self-explanatory metadata
                pom {
                    name = 'RudderStack Android SDK'
                    description = 'Extension for Android SDK of RudderStack for Multiple Process Apps. Steer your customer data.'
                    url = 'https://github.com/rudderlabs/rudder-sdk-android'

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'https://github.com/rudderlabs/rudder-sdk-android/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id = 'desusai7'
                            name = 'Desu Sai Venkat'
                            email ='venkat@rudderstack.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/rudderlabs/rudder-sdk-android.git'
                        developerConnection = 'scm:git:ssh://github.com:rudderlabs/rudder-sdk-android.git'
                        url = 'https://github.com/rudderlabs/rudder-sdk-android/tree/fix/multiple-process-access'
                    }
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(
            rootProject.ext["signing.keyId"],
            rootProject.ext["signing.key"],
            rootProject.ext["signing.password"],
    )
    sign publishing.publications
}